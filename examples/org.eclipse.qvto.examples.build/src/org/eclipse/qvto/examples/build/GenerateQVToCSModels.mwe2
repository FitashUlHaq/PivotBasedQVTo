/**
 * <copyright>
 *
 * Copyright (c) 2013 Willink Transformations, University of York and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *		Adolfo Sanchez-Barbudo Herrera (University of York) - initial API and implementation
 * 		
 * </copyright>
 *
 *
 */
module GenerateQVToCSModels

import org.eclipse.ocl.examples.build.utilities.*
import org.eclipse.ocl.examples.build.xtend.*
import org.eclipse.emf.mwe.utils.*
import org.eclipse.emf.mwe.utils.*
import org.eclipse.emf.ecore.resource.impl.*

/**
 *	Generate the Concrete Syntax Models used by the Xtext editors.
 */
var platformURI = ".."

/*
 * OCL Xtext models related configuration variables
 */
var oclXtextBaseProjectName = "org.eclipse.ocl.examples.xtext.base"
var oclXtextBaseProjectURI = "platform:/resource/${oclXtextBaseProjectName}"
var oclXtextBaseEcoreProjectPath = "model/BaseCST.ecore"
var oclXtextBaseEcoreURI = "${oclXtextBaseProjectURI}/${oclXtextBaseEcoreProjectPath}"
var oclXtextBaseVisitorPackage = "${oclXtextBaseProjectName}.util"
var oclXtextBaseVisitorName = "BaseCSVisitor"
var oclXtextBaseVisitorQualifiedName = "${oclXtextBaseVisitorPackage}.${oclXtextBaseVisitorName}"
var oclXtextBaseVisitablePackage = "${oclXtextBaseVisitorPackage}"
var oclXtextBaseVisitableName = "VisitableCS"

var essentialOclXtextProjectName = "org.eclipse.ocl.examples.xtext.essentialocl"
var essentialOclXtextProjectURI = "platform:/resource/${essentialOclXtextProjectName}"
var essentialOclXtextEcoreProjectPath = "model/EssentialOCLCST.ecore"
var essentialOclXtextEcoreURI = "${essentialOclXtextProjectURI}/${essentialOclXtextEcoreProjectPath}"
var essentialOclXtextVisitorPackage = "${essentialOclXtextProjectName}.util"
var essentialOclXtextVisitorName = "EssentialOCLCSVisitor"
var essentialOclXtextVisitorQualifiedName = "${essentialOclXtextVisitorPackage}.${essentialOclXtextVisitorName}"

/*
 * ImperativeOCL Xtext models related configuration variables
 */
var imperativeOclXtextProjectName = "org.eclipse.qvto.examples.xtext.imperativeocl"
var imperativeOclXtextProjectURI = "platform:/resource/${imperativeOclXtextProjectName}"
var imperativeOclXtextProjectPrefix = "ImperativeOCLCS"
var imperativeOclXtextSuperProjectName = "${essentialOclXtextProjectName}"
var imperativeOclXtextSuperProjectPrefix = "EssentialOCL" // FIXME Essential CS2AS visitors don't currently comply with naming convention which includes CS in their names
var imperativeOclXtextEcoreProjectPath = "model/${imperativeOclXtextProjectPrefix}.ecore"
var imperativeOclXtextEcoreURI = "${imperativeOclXtextProjectURI}/${imperativeOclXtextEcoreProjectPath}"
var imperativeOclXtextGenModelProjectPath = "model/${imperativeOclXtextProjectPrefix}.genmodel"
var imperativeOclXtextGenModelPath = "${imperativeOclXtextProjectName}/${imperativeOclXtextGenModelProjectPath}"
var imperativeOclXtextModelJavaFolder = "emf-gen"

/*
 * QVTOperational Xtext models related configuration variables
 */
var qvtOperationalXtextProjectName = "org.eclipse.qvto.examples.xtext.qvtoperational"
var qvtOperationalXtextProjectURI = "platform:/resource/${qvtOperationalXtextProjectName}"
var qvtOperationalXtextProjectPrefix = "QVTOperationalCS"
var qvtOperationalXtextSuperProjectName = "${imperativeOclXtextProjectName}"
var qvtOperationalXtextSuperProjectPrefix = "${imperativeOclXtextProjectPrefix}"
var qvtOperationalXtextEcoreProjectPath = "model/${qvtOperationalXtextProjectPrefix}.ecore"
var qvtOperationalXtextEcoreURI = "${qvtOperationalXtextProjectURI}/${qvtOperationalXtextEcoreProjectPath}"
var qvtOperationalXtextGenModelProjectPath = "model/${qvtOperationalXtextProjectPrefix}.genmodel"
var qvtOperationalXtextGenModelPath = "${qvtOperationalXtextProjectName}/${qvtOperationalXtextGenModelProjectPath}"
var qvtOperationalXtextModelJavaFolder = "emf-gen"

Workflow {
	bean = ResourceSetImpl : resourceSet {}
    bean = StandaloneSetup { resourceSet = resourceSet
    	platformUri = platformURI	// Common path from which...
    	scanClassPath = true		// ... a scan finds all classes
    	    	
    	uriMap = Mapping {
    		from = "platform:/plugin/org.eclipse.ocl.examples.build/"
    		to = "platform:/resource/org.eclipse.ocl.examples.build/"
    	}
    }
    bean = GenModelSetup { resourceSet = resourceSet }
    bean = UMLGenModelSetup { resourceSet = resourceSet }
    
//    component = DerivedVisitorPatternWeaver {
//    	resourceSet = resourceSet
//    	in = imperativeOclXtextEcoreURI
//    	in = essentialOclXtextEcoreURI
//    	in = oclXtextBaseEcoreURI
//    	rootVisitorInterfaceName = oclXtextBaseVisitorName
//    	rootVisitorInterfaceQualifiedName = oclXtextBaseVisitorQualifiedName
//    	superVisitorInterfaceName = essentialOclXtextVisitorName
//    	superVisitorInterfaceQualifiedName = essentialOclXtextVisitorQualifiedName
//    	derivedVisitorInterfaceName = "${imperativeOclXtextProjectPrefix}Visitor"
//    	derivedVisitorInterfaceQualifiedName = "${imperativeOclXtextProjectName}.imperativeoclcs.util.${imperativeOclXtextProjectPrefix}Visitor"
//    }

//    component = DerivedVisitorPatternWeaver {
//    	resourceSet = resourceSet
//    	in = qvtOperationalXtextEcoreURI
//    	in = essentialOclXtextEcoreURI
//    	in = oclXtextBaseEcoreURI
//    	rootVisitorInterfaceName = oclXtextBaseVisitorName
//    	rootVisitorInterfaceQualifiedName = oclXtextBaseVisitorQualifiedName
//    	superVisitorInterfaceName = "${imperativeOclXtextProjectPrefix}Visitor"
//    	superVisitorInterfaceQualifiedName = "${imperativeOclXtextProjectName}.imperativeoclcs.util.${imperativeOclXtextProjectPrefix}Visitor"
//    	derivedVisitorInterfaceName = "${qvtOperationalXtextProjectPrefix}Visitor"
//    	derivedVisitorInterfaceQualifiedName = "${qvtOperationalXtextProjectName}.qvtoperationalcs.util.${qvtOperationalXtextProjectPrefix}Visitor"
//    }
    
    component = GenerateModel { resourceSet = resourceSet skipOnErrors = true
		genModel = imperativeOclXtextGenModelPath
		clearResourceSet = false 	
    }
    
    component = GenerateModel { resourceSet = resourceSet skipOnErrors = true
		genModel = qvtOperationalXtextGenModelPath
		clearResourceSet = false 	
    }
    
    component = GenerateCS2ASVisitors { resourceSet = resourceSet
    	genModelFile = imperativeOclXtextGenModelProjectPath // FIXME change this when changing from automatically generated MM to a manual one located in /model
    	javaFolder = imperativeOclXtextModelJavaFolder
    	projectName = imperativeOclXtextProjectName
    	projectPrefix = imperativeOclXtextProjectPrefix
    	superProjectName = imperativeOclXtextSuperProjectName
    	superProjectPrefix = imperativeOclXtextSuperProjectPrefix
    	superVisitorPackageName = essentialOclXtextVisitorPackage // FIXME if Essential OCL aligns with conventions
    	superVisitorClassName = essentialOclXtextVisitorName      // FIXME if Essential OCL aligns with conventions
    	visitablePackageName = oclXtextBaseVisitablePackage
    	visitableClassName = oclXtextBaseVisitableName
    }
    
    component = GenerateCS2ASVisitors { resourceSet = resourceSet
    	javaFolder = qvtOperationalXtextModelJavaFolder
    	projectName = qvtOperationalXtextProjectName
    	projectPrefix = qvtOperationalXtextProjectPrefix
    	superProjectName = qvtOperationalXtextSuperProjectName
    	superProjectPrefix = qvtOperationalXtextSuperProjectPrefix
    	visitablePackageName = oclXtextBaseVisitablePackage
    	visitableClassName = oclXtextBaseVisitableName
    }
}